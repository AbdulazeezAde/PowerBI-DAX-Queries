Sales sparkline = 
// Static line color - use %23 instead of # for Firefox compatibility (Measure Derived from Eldersveld Modified by Kolosko)
VAR LineColour = "%2320E2D7"
VAR PointColour = "%23333333"
VAR Defs = "<defs>
    <linearGradient id='grad' x1='0' y1='25' x2='0' y2='50' gradientUnits='userSpaceOnUse'>
        <stop stop-color='#2320E2D7' offset='0' />
        <stop stop-color='#2320E2D7' offset='0.3' />
        <stop stop-color='#333333' offset='1' />
    </linearGradient>
</defs>"
// "Date" field used in this example along the X axis
VAR XMinDate = MIN('Date'[Start of Month])
VAR XMaxDate = MAX('Date'[Start of Month])

// Obtain overall min and overall max measure values when evaluated for each date
VAR YMinValue = MINX(Values('Date'[Start of Month]),CALCULATE([TotalSales] ))
VAR YMaxValue = MAXX(Values('Date'[Start of Month]),CALCULATE([TotalSales] ))

// Build table of X & Y coordinates and fit to 50 x 150 viewbox
VAR SparklineTable = ADDCOLUMNS(
    SUMMARIZE('Date','Date'[Start of Month]),
        "X",INT(150 * DIVIDE('Date'[Start of Month] - XMinDate, XMaxDate - XMinDate)),
        "Y",INT(50 * DIVIDE([TotalSales] - YMinValue,YMaxValue - YMinValue)))

// Concatenate X & Y coordinates to build the sparkline
VAR Lines = CONCATENATEX(SparklineTable,[X] & "," & 50-[Y]," ", 'Date'[Start of Month])

// Last data points on the line
VAR LastSparkYValue = MAXX( FILTER(SparklineTable, 'Date'[Start of Month] = XMaxDate), [Y])
VAR LastSparkXValue = MAXX( FILTER(SparklineTable, 'Date'[Start of Month] = XMaxDate), [X])

// Add to SVG, and verify Data Category is set to Image URL for this measure
VAR SVGImageURL = 
    "data:image/svg+xml;utf8," & 
    --- gradient---
    "<svg xmlns='http://www.w3.org/2000/svg' x='0px' y='0px' viewBox='-7 -7 164 64'>" & Defs & 
     "<polyline fill='url(#grad)' fill-opacity='0.3' stroke='transparent' 
      stroke-width='0' points=' 0 50 " & Lines & 
      " 150 150 Z '/>" &
    --- Lines---
    "<polyline 
        fill='transparent' stroke='" & LineColour & "' 
        stroke-linecap='round' stroke-linejoin='round' 
        stroke-width='1' points=' " & Lines & 
      " '/>" &
    --- Last Point---
        "<circle cx='"& LastSparkXValue & "' cy='" & 50 - LastSparkYValue & "' r='4' stroke='" & LineColour & "' stroke-width='1' fill='" & PointColour & "' />" &
        "</svg>"
RETURN SVGImageURL
